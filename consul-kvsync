#!/usr/bin/env perl

# consul-kvsync - keep a KV tree in sync with another
# Copyright (c) 2015 Robert Norris. MIT license.

use 5.014;
use warnings;
use strict;

use AnyEvent;
use AnyEvent::Consul 0.005;
use AnyEvent::Log;
use Sys::Syslog qw(openlog);
use Date::Format qw(time2str);

my $at_terminal = -t \*STDIN;

if (!$at_terminal || $ENV{CONSUL_KVSYNC_SYSLOG}) {
  openlog("consul-kvsync", "ndelay,pid", "local1");
  AnyEvent::Log::ctx->log_to_syslog("local1");
  AnyEvent::Log::ctx->fmt_cb(sub {
    my $str = $_[3];
    $str =~ s/\n(?=.)/\n+ /g;
    [$_[2], $str];
  });
}
else  {
  AnyEvent::Log::ctx->log_to_warn;
  AnyEvent::Log::ctx->fmt_cb(sub {
    sprintf "%s %s\n", time2str("%Y-%m-%dT%H:%M:%S", time), $_[3];
  });
}
my $log = AnyEvent::Log::logger("notice");

# args are shaped like: [kv-path@remote-dc]
# changes under the path on the remote are copied to the local (unnamed) dc on
# the same path

# path => source dc
my %dc_for_path;
for my $pathspec (@ARGV) {
  my ($path, $dc) = split '@', $pathspec;
  unless (defined $path && defined $dc) {
    die "E: invalid path: $pathspec\n";
  }
  if (exists $dc_for_path{$path}) {
    die "E: already declared path: $pathspec\n";
  }
  $dc_for_path{$path} = $dc;
}
my @paths = sort keys %dc_for_path;

unless (@paths) {
  if ($at_terminal) {
    say "usage: $0 [kv-path\@dc...]";
  }
  else {
    $log->("no paths configured, sleeping");
    while (1) { sleep 3600 }
  }
  exit 1;
}

# keys present on the destination last time we looked
#
# path => {
#   path/a => 1
#   path/b => 1
#   ...
my %dest_keys;


my $cv = AnyEvent->condvar;
my $kvapi = AnyEvent::Consul->kv(timeout => 60);

for my $path (@paths) {
  $log->("path: starting dest watcher");
  setup_dest_watcher($path, 0);

  my $dc = $dc_for_path{$path};
  $log->("$path\@$dc: starting source watcher");
  setup_source_watcher($path, $dc, 0);
}

$cv->recv;


sub setup_dest_watcher {
  my ($path, $index) = @_;

  # if a specific index is provided, then we should block until we move past
  # that index. index 0 is to get the initial state, and should just return
  my @extra;
  if ($index > 0) {
    @extra = (
      index => $index,
      wait => '30s',
    );
  }

  $kvapi->get_all(
    $path,
    @extra,
    cb => sub { dest_change_handler($path, $index, @_) },
    error_cb => sub { dest_error_handler($path, @_) },
  );
}

sub dest_error_handler {
  my ($path,  $msg) = @_;

  $log->("E: $path: $msg");

  my $sleep_guard; $sleep_guard = AnyEvent->timer(after => "1s", cb => sub {
    $log->("$path: restarting dest watcher");
    setup_dest_watcher($path, 0);
    undef $sleep_guard;
  });
}

sub dest_change_handler {
  my ($path, $prev_index, $kv, $meta) = @_;

  my $index = $meta->index;

  if ($index > $prev_index) {
    $dest_keys{$path} = { map { $_->key => 1 } @$kv };

    $log->(
      "$path: $prev_index -> $index, "
      .scalar(@$kv)." keys exist"
    );
  }

  setup_dest_watcher($path, $index);
}


sub setup_source_watcher {
  my ($path, $dc, $index) = @_;

  # if a specific index is provided, then we should block until we move past
  # that index. index 0 is to get the initial state, and should just return
  my @extra;
  if ($index > 0) {
    @extra = (
      index => $index,
      wait => '30s',
    );
  }

  $kvapi->get_all(
    $path,
    dc => $dc,
    @extra,
    cb => sub { source_change_handler($path, $dc, $index, @_) },
    error_cb => sub { source_error_handler($path, $dc, @_) }
  );
}

sub source_error_handler {
  my ($path, $dc, $msg) = @_;

  $log->("E: $path\@$dc: $msg");

  my $sleep_guard; $sleep_guard = AnyEvent->timer(after => "1s", cb => sub {
    $log->("$path\@$dc: restarting source watcher");
    setup_source_watcher($path, $dc, 0);
    undef $sleep_guard;
  });
}

sub source_change_handler {
  my ($path, $dc, $prev_index, $kv, $meta) = @_;

  my $index = $meta->index;

  if ($index > $prev_index) {
    my ($created, $updated, $deleted) = (0,0,0);
    my %source_keys = map { $_->key => 1 } @$kv;

    for my $key (keys %{$dest_keys{$path}}) {
      next if $source_keys{$key};

      $kvapi->delete(
        $key,
        cb => sub {
          my ($success, $meta) = @_;
          $log->("$path: deleted $key, ".($success ? "succeeded" : "FAILED"));
        },
      );

      $deleted++;
    }

    my @changed = grep { $_->modify_index > $prev_index } @$kv;

    for my $change (@changed) {
      my $key = $change->key;
      my $is_create = !exists $dest_keys{$path}{$key};

      $kvapi->put(
        $key,
        $change->value,
        cb => sub {
          my ($success, $meta) = @_;
          $log->(
            "$path\@$dc: ".($is_create ? "created" : "updated")
            ." $key = ".$change->value.", "
            .($success ? "succeeded" : "FAILED")
          );
        },
      );

      if ($is_create) {
        $created++;
      }
      else {
        $updated++;
      }
    }

    $log->(
      "$path\@$dc: $prev_index -> $index, "
      ."$created created, $updated updated, $deleted deleted"
    );

  }

  setup_source_watcher($path, $dc, $index);
}

1;
